plugins {
    id 'java'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '1.0.11'
}

version = rootProject.mod_version

neoForge {
    neoFormVersion = "1.21.1-20240808.144430"
}

dependencies {
    implementation "dev.kosmx.player-anim:anim-core:${rootProject.player_animator_version}"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
}

test {
    useJUnitPlatform()
}

//-------- publishing --------

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            // add all the jars that should be included when publishing to maven

            artifactId = 'emotesAPI'

            artifact(jar)
            artifact(sourcesJar)
            artifact(javadocJar)


            pom.withXml {
                def depsNode = asNode().appendNode("dependencies")

                def animatorNode = depsNode.appendNode("dependency")
                animatorNode.appendNode("groupId", "dev.kosmx.player-anim")
                animatorNode.appendNode("artifactId", "anim-core")
                animatorNode.appendNode("version", rootProject.player_animator_version)
                animatorNode.appendNode("scope", "compile")

            }

            pom{
                name = "emotesApi"
                description = "Minecraft Emotecraft api"
                url = 'https://github.com/KosmX/emotes'
                developers {
                    developer {
                        id = 'kosmx'
                        name = 'KosmX'
                        email = 'kosmx.mc@gmail.com'
                    }
                }

                licenses{
                    license{
                        name = "CC-BY-4.0 License"
                        url = "https://creativecommons.org/licenses/by/4.0/legalcode"
                    }
                }

                scm {
                    connection = 'scm:git:github.com/kosmx/emotes.git'
                    developerConnection = 'scm:git:github.com/kosmx/emotes.git'
                    url = 'https://github.com/KosmX/emotes'
                }
            }
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        if (project.keysExists) {
            repositories {
                maven {
                    url = 'https://maven.kosmx.dev/'
                    credentials {
                        username = 'kosmx'
                        password = project.keys.kosmx_maven
                    }
                }
            }
        }
        else {
            mavenLocal()
        }
    }
}